c_import include/raylib.h
c_link lib/libraylib.a -lGL -lm -lpthread -ldl -lrt -lwayland-client -lwayland-cursor -lwayland-egl -lxkbcommon

struct Paddle {
    int x
    int y
    int width
    int height
    int speed
}

struct Ball {
    int x
    int y
    int radius
    int velX
    int velY
}

Paddle createPaddle(int startX, int startY) {
    return Paddle(startX, startY, 20, 120, 12)
}

Ball createBall(int startX, int startY) {
    return Ball(startX, startY, 20, 8, 6)
}

Paddle updatePaddle(Paddle p, int upKey, int downKey) {
    int newY = p.y
    if (IsKeyDown(upKey)) {
        newY = p.y - p.speed
    }
    if (IsKeyDown(downKey)) {
        newY = p.y + p.speed
    }
    if (newY < 0) {
        newY = 0
    }
    if (newY + p.height > 600) {
        newY = 600 - p.height
    }
    return Paddle(p.x, newY, p.width, p.height, p.speed)
}

Ball updateBall(Ball b) {
    int newX = int(b.x + b.velX)
    int newY = int(b.y + b.velY)
    int newVelY = b.velY

    if (newY < b.radius) {
        newY = b.radius
        newVelY = 0 - newVelY
    }
    if (newY > 600 - b.radius) {
        newY = 600 - b.radius
        newVelY = 0 - newVelY
    }

    return Ball(newX, newY, b.radius, b.velX, newVelY)
}

Ball checkLeftPaddleCollision(Ball b, Paddle p) {
    if (b.x - b.radius <= p.x + p.width && b.x >= p.x && b.y >= p.y && b.y <= p.y + p.height) {
        return Ball(b.x + 12, b.y, b.radius, 0 - b.velX, b.velY)
    }
    return b
}

Ball checkRightPaddleCollision(Ball b, Paddle p) {
    if (b.x + b.radius >= p.x && b.x <= p.x + p.width && b.y >= p.y && b.y <= p.y + p.height) {
        return Ball(b.x - 12, b.y, b.radius, 0 - b.velX, b.velY)
    }
    return b
}

void main() {
    InitWindow(1200, 600, "Pong")
    SetTargetFPS(60)

    Color bgColor = Color(0, 0, 50, 255)
    Color paddleColor = Color(0, 255, 0, 255)
    Color ballColor = Color(255, 255, 100, 255)
    Color textColor = Color(255, 255, 255, 255)

    Paddle leftPaddle = createPaddle(30, 250)
    Paddle rightPaddle = createPaddle(1160, 250)
    Ball ball = createBall(600, 300)

    int leftScore = 0
    int rightScore = 0

    while (!WindowShouldClose()) {
        leftPaddle = updatePaddle(leftPaddle, 87, 83)
        rightPaddle = updatePaddle(rightPaddle, 265, 264)
        ball = updateBall(ball)

        ball = checkLeftPaddleCollision(ball, leftPaddle)
        ball = checkRightPaddleCollision(ball, rightPaddle)

        if (ball.x < ball.radius+2) {
            rightScore = rightScore + 1
            ball = createBall(600, 300)
        }
        if (ball.x > 1200 - ball.radius-2) {
            leftScore = leftScore + 1
            ball = createBall(600, 300)
        }

        BeginDrawing()
        ClearBackground(bgColor)

        DrawRectangle(leftPaddle.x, leftPaddle.y, leftPaddle.width, leftPaddle.height, paddleColor)
        DrawRectangle(rightPaddle.x, rightPaddle.y, rightPaddle.width, rightPaddle.height, paddleColor)

        DrawCircle(ball.x, ball.y, ball.radius, ballColor)

        DrawText("Pong Game", 450, 20, 40, textColor)
        DrawText(f"Left: {leftScore}", 100, 50, 30, textColor)
        DrawText(f"Right: {rightScore}", 1000, 50, 30, textColor)

        EndDrawing()
    }

    CloseWindow()
    println("Thanks for playing Pong in Fr!")
}
