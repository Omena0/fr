c_import include/raylib.h
c_link lib/libraylib.a -lGL -lm -lpthread -ldl -lrt -lwayland-client -lwayland-cursor -lwayland-egl -lxkbcommon

struct Paddle {
    float x
    float y
    int width
    int height
    float speed
}

struct Ball {
    float x
    float y
    float radius
    float velX
    float velY
}

Paddle createPaddle(int startX, int startY) {
    float fx = float(startX)
    float fy = float(startY)
    return Paddle(fx, fy, 20, 120, 720.0)
}

Ball createBall(int startX, int startY) {
    float fx = float(startX)
    float fy = float(startY)
    return Ball(fx, fy, 20.0, -300.0, 100.0)
}

Paddle updatePaddle(Paddle p, int upKey, int downKey) {
    float dt = GetFrameTime()

    float newY = p.y
    if (IsKeyDown(upKey)) {
        newY = p.y - p.speed * dt
    }
    if (IsKeyDown(downKey)) {
        newY = p.y + p.speed * dt
    }
    if (newY < 0) {
        newY = 0
    }
    if (newY + p.height > 600) {
        newY = 600 - p.height
    }
    return Paddle(p.x, newY, p.width, p.height, p.speed)
}

Ball updateBall(Ball b) {
    float dt = GetFrameTime()

    float newX = b.x + b.velX * dt
    float newY = b.y + b.velY * dt
    float newVelY = b.velY

    if (newY < b.radius) {
        newY = b.radius
        newVelY = 0.0 - newVelY
    }
    if (newY > 600 - b.radius) {
        newY = 600 - b.radius
        newVelY = 0.0 - newVelY
    }

    return Ball(newX, newY, b.radius, b.velX, newVelY)
}

Ball checkLeftPaddleCollision(Ball b, Paddle p) {
    if (b.x - b.radius <= p.x + p.width && b.x >= p.x && b.y >= p.y && b.y <= p.y + p.height) {
        return Ball(b.x + 12.0, b.y, b.radius, 0.0 - b.velX, b.velY)
    }
    return b
}

Ball checkRightPaddleCollision(Ball b, Paddle p) {
    if (b.x + b.radius >= p.x && b.x <= p.x + p.width && b.y >= p.y && b.y <= p.y + p.height) {
        return Ball(b.x - 12.0, b.y, b.radius, 0.0 - b.velX, b.velY)
    }
    return b
}

void main() {
    InitWindow(1200, 600, "Pong")
    SetTargetFPS(60)

    Color bgColor = Color(0, 0, 50, 255)
    Color paddleColor = Color(0, 255, 0, 255)
    Color ballColor = Color(255, 255, 100, 255)
    Color textColor = Color(255, 255, 255, 255)

    Paddle leftPaddle = createPaddle(30, 250)
    Paddle rightPaddle = createPaddle(1160, 250)
    Ball ball = createBall(600, 300)

    int leftScore = 0
    int rightScore = 0

    while (!WindowShouldClose()) {
        leftPaddle = updatePaddle(leftPaddle, 265, 264)
        rightPaddle = updatePaddle(rightPaddle, 87, 83)
        ball = updateBall(ball)

        ball = checkLeftPaddleCollision(ball, leftPaddle)
        ball = checkRightPaddleCollision(ball, rightPaddle)

        if (ball.x < ball.radius+2) {
            leftScore = leftScore + 1
            ball = createBall(600, 300)
        }
        if (ball.x > 1200 - ball.radius-2) {
            rightScore = rightScore + 1
            ball = createBall(600, 300)
        }

        BeginDrawing()
        ClearBackground(bgColor)

        DrawRectangle(int(leftPaddle.x), int(leftPaddle.y), leftPaddle.width, leftPaddle.height, paddleColor)
        DrawRectangle(int(rightPaddle.x), int(rightPaddle.y), rightPaddle.width, rightPaddle.height, paddleColor)

        DrawCircle(int(ball.x), int(ball.y), ball.radius, ballColor)

        DrawText("Pong Game", 450, 20, 40, textColor)
        DrawText(f"Left: {leftScore}", 100, 50, 30, textColor)
        DrawText(f"Right: {rightScore}", 1000, 50, 30, textColor)

        EndDrawing()
    }

    CloseWindow()
    println("Thanks for playing Pong in Fr!")
}
